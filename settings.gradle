plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

rootProject.name = 'java-version'

includeBuild('probe') {
    name = 'java-probe'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }

    //@formatter:off
    versionCatalogs.register('libs') {
        library 'gson',  'com.google.code.gson', 'gson'        version '2.10.1'      // > 2.9.0 needs Java 7
        library 'jopt',  'net.sf.jopt-simple',   'jopt-simple' version '6.0-alpha-3' // Java 8
        library 'jtar',  'org.kamranzafar',      'jtar'        version '2.3'
        library 'probe', 'net.minecraftforge',   'java-probe'  withoutVersion()
    }
    //@formatter:on
}
