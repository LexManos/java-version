import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
}

final projectDisplayName = 'Java Version'
final projectVendor = 'Forge Development LLC'
description = 'Java Version management using Disco'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println "Version: $version"

java {
    // Currently JOpt is the limiting, requiring java 8
    // But also toolchains don't support building for anything lower then 8
    toolchain.languageVersion = JavaLanguageVersion.of 8
    withSourcesJar()
}

tasks.register('createJavaProbeClass', buildsrc.ClassGeneratorTask)

tasks.register('javaProbeJar', Jar) {
    destinationDirectory = layout.buildDirectory.dir('libs')
    archiveBaseName = project.name
    archiveClassifier = 'probe'

    manifest {
        attributes([
            'Main-Class'            : 'JavaProbe',
            'Specification-Title'   : 'Java Probe',
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : 'javaprobe',
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ])
    }

    from createJavaProbeClass.outputs.files
}

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jtar
    implementation javaProbeJar.outputs.files
}

license {
    header rootProject.file('LICENSE-header.txt')
    newLine false
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Main-Class'           : 'net.minecraftforge.java_version.Main',
            'Automatic-Module-Name': 'net.minecraftforge.java_version',
            'Sealed'               : true
        ])
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/java_version/')
    }

    from createJavaProbeClass.outputs.files
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation = true
    relocationPrefix = 'net.minecraftforge.java_version.shadow'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

changelog {
    from '1.0'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java

        artifactId = project.name
        artifact javaProbeJar

        pom { pom ->
            name = projectDisplayName
            description = project.description

            gradleutils.pom.setGitHubDetails pom

            licenses {
                license gradleutils.pom.licenses.LGPLv2_1
            }

            developers {
                developer gradleutils.pom.developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadJavadoc = downloadSources = true }
