import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    alias libs.plugins.licenser
    alias libs.plugins.gradleutils
    alias libs.plugins.shadow
}

final displayName = 'Java Version'
final displayVendor = 'Forge Development LLC'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println('Version: ' + version)

configurations {
    var probe = register('probe') {
        canBeConsumed = false
        canBeResolved = true
    }

    named('implementation') {
        extendsFrom probe.get()
    }
}

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jtar
    probe project(path: ':probe', configuration: 'jar')
}

license {
    header project.file('LICENSE-header.txt')
    newLine false
}

java {
    // Currently JOpt is the limiting, requiring java 8
    // But also toolchains don't support building for anything lower then 8
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation = true
    relocationPrefix = 'net.minecraftforge.java_version.reloc'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Main-Class'           : 'net.minecraftforge.java_version.Main',
            'Automatic-Module-Name': 'net.minecraftforge.java_version',
            'Sealed'               : true
        ])
        attributes([
            'Specification-Title'   : displayName,
            'Specification-Vendor'  : displayVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : displayName,
            'Implementation-Vendor' : displayVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/java_version/')
    }

    from provider {
        configurations.probe.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.named('compileJava', JavaCompile) {
    options.encoding = 'UTF-8'
}

changelog {
    from '1.0'
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        artifactId = project.name
        from components.java

        artifact(provider { configurations.probe.singleFile }) {
            classifier = 'probe'
        }

        pom { pom ->
            name = project.name
            description = 'Minecraft Maven Repo Generator'
            url = 'https://github.com/MinecraftForge/java-version'

            gradleutils.pom.gitHubDetails = pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadJavadoc = downloadSources = true }
