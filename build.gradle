import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.licenser' version '1.2.0'
    id 'net.minecraftforge.gradleutils' version '2.5.1'
    id 'com.gradleup.shadow' version '8.3.6'
}

final projectDisplayName = 'Java Version'
final projectVendor = 'Forge Development LLC'
group = 'net.minecraftforge'
version = gitversion.tagOffset

println('Version: ' + version)

configurations {
    resolvable('probe')

    runtimeClasspath {
        extendsFrom probe
    }
}

dependencies {
    implementation libs.jopt
    implementation libs.gson
    implementation libs.jtar
    probe libs.probe
}

license {
    header project.file('LICENSE-header.txt')
    newLine false
}

java {
    // Currently JOpt is the limiting, requiring java 8
    // But also toolchains don't support building for anything lower then 8
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

final probeJar = objects.fileProperty().fileProvider provider {
    configurations.probe.singleFile
} as Provider<File>

tasks.named('shadowJar', ShadowJar) {
    // NOTE: Since JavaProbe is in the default package, it does NOT get relocated.
    enableRelocation = true
    relocationPrefix = 'net.minecraftforge.java_version.shadow'
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Main-Class'           : 'net.minecraftforge.java_version.Main',
            'Automatic-Module-Name': 'net.minecraftforge.java_version',
            'Sealed'               : true
        ])
        attributes([
            'Specification-Title'   : projectDisplayName,
            'Specification-Vendor'  : projectVendor,
            'Specification-Version' : gitversion.info.tag,
            'Implementation-Title'  : projectDisplayName,
            'Implementation-Vendor' : projectVendor,
            'Implementation-Version': project.version
        ], 'net/minecraftforge/java_version/')
    }

    from provider {
        zipTree probeJar
    }
}

tasks.named('compileJava', JavaCompile) {
    dependsOn gradle.includedBuild('java-probe').task(':createClass')
    options.encoding = 'UTF-8'
}

changelog {
    from '1.0'
}

publishing {
    publications.register('mavenJava', MavenPublication).configure {
        artifactId = project.name
        from components.java
        artifact(probeJar) {
            classifier = 'probe'
        }

        pom { pom ->
            name = project.name
            description = projectDisplayName

            gradleutils.pom.gitHubDetails = pom

            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.developers.LexManos
            }
        }
    }

    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

idea.module { downloadJavadoc = downloadSources = true }
